#map read bundles/mapa/map.map;
#read bundles/mapeditor/data.tin;
#map goto 1;
#map at {1} {#map get roomdata MapStampValue};

#nop ACCEPT NEW CONNECTIONS;
#action {<PORT> New connection: %* D%d.} {
	#list userList add {%2};
	#var userDetails[%2][host] {%1};
#nop wyslij checksum;   
    #map at {1} {#map get roomdata vrd};
    #line ignore #sh {<118> Sending checksum to <128>%2<088>};    
    #port send %2 CHECKSUM $vrd;
};

#action {<PORT> Closing connection to %* D%d.} {
	#list userList find %2 ulfv;
	#list userList del $ulfv;
};

#alias {broadcast} {
    #line ignore #sh {<128>broadcasting <138> %0<088>};
	#foreach {*userList[]} {user} {#port send $userList[$user] {%0};};
};


#nop #######################################################;
#nop #######################################################;
#nop #######################################################;
#nop #######################################################;

#alias mapsave {
	#map at {1} {#map get roomdata vroomdata};
	#math vroomdata {$vroomdata + 1};
	#map at {1} {#map set roomdata $vroomdata};
    #map at {1} {#map get roomdata MapStampValue};
	#map write bundles/mapa/map.map;
};

#nop ###################################;
#nop tworze pokoj w mapce glownej, zapisuje, wysylam komende wszystkim;
#nop ###################################;
#unevent {MAP CREATE ROOM} {
    #map at {%0} {#map set roomsymbol { O }};
};

#action {<PORT> MCREATE %d %w} {
	#map at {%1} {#map dig %2};
	broadcast MCREATE %1 %2;
    mapsave;
};

#action {<PORT> MCREATENEW %d %w} {
    #map at {%1} {#map dig %2 new};
    broadcast MCREATENEW %1 %2;
    mapsave;
};


#action {<PORT> DEL %d %w} {
    #map goto %1;
    #map at {%1} {#map del %2};
    broadcast DEL %1 %2;
    mapsave;
};

#action {<PORT> MEXIT %d %w %*} {
    #map goto %1;
    #map exit %2 command {%3};

    #switch {"%3"} {
        #case {"sp"} {#var mode {trap}};
        #case {"spojrz"} {#var mode {trap}};
        #case {"wyjscie"} {#var mode {instant}};
        #case {"przeskocz wyrwe"} {#var mode {instant}};
        #case {"wejdz na gore"} {#var mode {overtime}};
        #case {"przejdz po drzewie"} {#var mode {overtime}};
        #case {"wejdz na partner"} {#var mode {overtime}};
        #case {"wespnij sie na gore"} {#var mode {overtime}};
        #case {"wespnij sie na mur"} {#var mode {overtime}};                
        #case {"przeplyn na wysepke"} {#var mode {overtime}};
        #case {"przeplyn na plaze"} {#var mode {overtime}};
        #case {"przeplyn na polnoc"} {#var mode {overtime}};
        #case {"przeplyn na poludnie"} {#var mode {overtime}};
        #case {"przeplyn rzeke"} {#var mode {overtime}};
        #case {"przeplyn pod kratka"} {#var mode {overtime}};
        #case {"zejdz na dol"} {#var mode {overtime}};
        #default {#var mode {instant}};
    };
    #switch {"$mode"} {
        #case {"instant"} {#map exit %2 color <bfb>};
        #case {"trap"} {#map exit %2 color <faa>};
        #case {"overtime"} {#map exit %2 color <bbf>};
    };

    #map move %2;
    #map get roomvnum tmproomvnum;
    #map undo;
    #map link {%3} $tmproomvnum;
    broadcast MEXIT %1 %2 $tmproomvnum %3;
    mapsave;
};

#action {<PORT> LONG %d %w} {
    #map goto %1;
    #line substitute variables {#map insert %2 void};
    broadcast LONG %1 %2;
    mapsave;
};

#action {<PORT> SHORT %d %w} {
    #map goto %1;
    #line substitute variables {#map uninsert %2 void};
    broadcast SHORT %1 %2;
    mapsave;
};

#action {<PORT> VOID %d} {
    #map goto %1;
    #line substitute variables {
        #map roomflag void on;
        #map set roomsymbol;
    };
    broadcast VOID %1;
    mapsave;
};

#action {<PORT> UNHIDE %d %w} {
    #map goto %1;
    #line substitute variables {#map exitflag %2 hide off};
    #map exit %2 color;
    broadcast UNHIDE %1 %2;
    mapsave;
};

#action {<PORT> HIDE %d %w} {
    #map goto %1;
    #line substitute variables {#map exitflag %2 hide on};
    #map exit %2 color <ffa>;
    broadcast HIDE %1 %2;
    mapsave;
};

#action {<PORT> NOTE %d %w %*} {
    #map goto %1;
    #map get roomdata vroomdata;
    #list vroomdata size vroomdatasize;

    #format timeStamp {%t} {%d%m%y %r};
    #unvar roomdatavalue;
    #var roomdatavalue[author] {%2};
    #var roomdatavalue[stamp] $timeStamp;
    #var roomdatavalue[message] {%3};
    #var roomdatavalue[type] {test};

    #if {$vroomdatasize > 0} {
        #math vr {$vroomdatasize + 1};
        #list vroomdata add {{$roomdatavalue}};
    } {
        #list vroomdata create;
        #list vroomdata add {{$roomdatavalue}};
    };
    #map set roomdata $vroomdata;
    #map set roomnote {data};

    broadcast NOTE %1 %2 %3;
    mapsave;
} {1};

#action {<PORT> BIND %d %w {F1|F2|F3} %*} {
        #map goto %1;
        #map get roomdata vroomdata;
        #list vroomdata size vroomdatasize;

        #format timeStamp {%t} {%d%m%y %r};
        #var roomdatavalue[author] %2;
        #var roomdatavalue[stamp] $timeStamp;
        #var roomdatavalue[message] {%4};
        #var roomdatavalue[type] {command};
        #var roomdatavalue[macrokey] {%3};

        #if {$vroomdatasize < 1} {
            #list vroomdata create;
            #list vroomdata add {{$roomdatavalue}};             
        } {
            #var searchforbind 0;
            #foreach {*vroomdata[]} {vr} {
                #if {"$vroomdata[$vr][type]" == "command"} {
                    #if {"$vroomdata[$vr][macrokey]" == "%3"} {
                        #var vroomdata[$vr][message] {%4};
                        #var roomdatavalue[author] %2;
                        #var roomdatavalue[stamp] $timeStamp;
                        #var searchforbind 1;
                    };
                };
            };
            #if {$searchforbind < 1} {
                #math vr {$vroomdatasize + 1};
                #list vroomdata add {{$roomdatavalue}};             
            };
        };

        #map set roomdata $vroomdata;
        #map set roomnote {data};
        broadcast BIND %1 %2 %3 %4;
        mapsave;
} {1};

#action {<PORT> ROOMSHORT %d %*} {
    #map goto %1;
    #map set roomdesc {%2};
    #map get roomsymbol vroomsymbol;
    #if {"$vroomsymbol" == "<faa>?<088>"} {
        #map set roomsymbol {};
    };
    broadcast ROOMSHORT %1 %2;
    mapsave;
};

#action {<PORT> ROOMEXITS %d %*} {
    #map goto %1;
    #map set roomname {%2};
    #map get roomcolor vroomcolor;
    #if {"$vroomcolor" == "<faa>"} {
        #map set roomcolor {};
    };
    broadcast ROOMEXITS %1 %2;
    mapsave;
};

#action {<PORT> LINKB %d %w %d} {
    #map at {%1} {
        #line substitute variables {
            #map link %2 %3 both;
        };
    };
    broadcast LINKB %1 %2 %3;
    mapsave;
} {1};

#action {<PORT> UNLINKB %d %w} {
    #map at {%1} {
        #line substitute variables {
            #map unlink %2 both;            
        };
    };
    broadcast UNLINKB %1 %2;
    mapsave;
} {1};

#action {<PORT> LINK %d %w %d} {
    #map at {%1} {
        #line substitute variables {
            #map link %2 %3;
        };
    };
    broadcast LINK %1 %2 %3;
    mapsave;
} {1};

#action {<PORT> UNLINK %d %w} {
    #map at {%1} {
        #line substitute variables {
            #map unlink %2;         
        };
    };
    broadcast UNLINK %1 %2;
    mapsave;    
} {1};

#nop ###################################;
#nop new event, subscribers' mapevents.tin should be reloaded;
#nop ###################################;
#action {<PORT> EVENT %d %*} {
    broadcast EVENT %1 %2;
    #map at {%1} {#map set roomsymbol { <168>@<088> }};
    #map at {%1} {#map set roomarea {QB %2}};

	broadcast EVENT %1 %2;
    mapsave;
};

#action {<PORT> UNEVENT %d} {
    #map at {%1} {#map set roomsymbol { O }};
    #map at {%1} {#map set roomarea};
    
    broadcast UNEVENT %1;
    mapsave;
} {1};

#action {<PORT> COMMAND %d %*} {
    #map at {%1} {#map set roomsymbol {<158>EXE<088>}};
    #map at {%1} {#map set roomarea {%2}};
	broadcast COMMAND %1 %2;
    mapsave;
};
#nop ####################################;
#nop POKOLORUJ KIERUNEK JAKO CUSTOM;
#nop ####################################;
#action {<PORT> SPECIAL %d %w} {
    #map at {%1} {#map exit %2 color <faa>};
    broadcast SPECIAL %1 %2;
    mapsave;
};
#action {<PORT> INSTANT %d %w} {
    #map at {%1} {#map exit %2 color <bfb>};
    broadcast INSTANT %1 %2;
    mapsave;
};
#action {<PORT> OVERTIME %d %w} {
    #map at {%1} {#map exit %2 color <bbf>};
    broadcast OVERTIME %1 %2;
    mapsave;
};
#action {<PORT> TRAP %d %w} {
    #map at {%1} {#map exit %2 color <faa>};
    broadcast TRAP %1 %2;
    mapsave;
};

#action {<PORT> PAT %d %w} {
    #map at {%1} {#map exit %2 color <fff>};
    broadcast PAT %1 %2;
    mapsave;
};

#nop ####################################;
#nop DODAJ SYMBOL I KOLOR;
#nop ####################################;
#action {<PORT> SYMBOL %d %S %S} {
    #map at {%1} {#map set roomsymbol {<%3>%2<088>}};
    broadcast SYMBOL %1 %2 %3;
    mapsave;
} {1};

#nop ####################################;
#nop USUN WSZYSTKO Z DANEGO POKOJU;
#nop ####################################;
#action {<PORT> CLEAR %d} {
    #map at {%1} {#map set roomsymbol { O }};
    #map at {%1} {#map set roomarea};
    #map at {%1} {#map set roomdata};
    #map at {%1} {#map set roomnote};
    broadcast CLEAR %1;
    mapsave;
};

#action {<PORT> TRAKT %d} {
    #map at {%1} {#map set roomsymbol {<fff> O <088>}};
    broadcast TRAKT %1;
    mapsave;
};