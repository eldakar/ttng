#nop ###########################################################;
#nop Event gmcp.char.info;
#nop ###########################################################;
#nop list event_handler_char_info add {markups_gmcp_char_info};
    #nop alias {markups_gmcp_char_info} {
	      #var _CHAR_INFO_ {%0};
        #if {"$_CHAR_INFO_[guild_lay]" == "Kultysci Chaosu"} {
        };
    };
#class markups read markups.tt;

#var markupsStatus off;
#alias {/a %w} {
	#map get roomvnum markupsRoomVnum;
	#map get roomsymbol _rs;
	#map get roomterrain _rt;
	#line sub variables {
		#class markups read markups.tt;
		#class markups open;

		#var markupsymbols[mob] {<FFA><aaa>MOB<088>};
		#var markupsymbols[ext] {<138> @ <088>};
		#var markupsymbols[rot] {<faa>;~;<088>};
		#if {$m[$markupsRoomVnum][rendered] > 0} {
			#nop line ignore #sh {<178>Highlighter: <038>Juz podswietlam ten pokoj!};
		} {
			#var m[$markupsRoomVnum][rendered] {1};
			#var m[$markupsRoomVnum][type] {%1};
			#var m[$markupsRoomVnum][symbol] {$_rs};
			#var m[$markupsRoomVnum][terrain] {$_rt};
			#map set roomsymbol {$markupsymbols[%1]};
			#line ignore #sh {<178>Highlighter: <028>Dodalem %1};
		};
		#class markups close;
		#class markups write markups.tt;
	};
} {1};

#alias {/e ext %*} {
	#map get roomvnum markupsRoomVnum;
	#map get roomsymbol _rs;
	#map get roomterrain _rt;
	#line sub variables {
		#class markups read markups.tt;
		#class markups open;

		#var m[$markupsRoomVnum][rendered] {1};
		#var m[$markupsRoomVnum][type] {ext};
		#var m[$markupsRoomVnum][direction][%1] {%1};
		#var m[$markupsRoomVnum][symbol] {$_rs};
		#var m[$markupsRoomVnum][terrain] {$_rt};
		#map set roomsymbol {<138> @ <088>};
		#map exit %1 color <138>;
		#nop line ignore #sh {<178>Highlighter: <028>Dodalem ext %1};

		#class markups close;
		#class markups write markups.tt;
	};
} {1};

#alias {/gettrolle} {
	#foreach {*m[]} {_m} {
		#if {"$m[$_m][type]" == "mob"} {
			#map find $_m;
			#path save forward trollpath;
			#sh {room:$_m path:$trollpath};
		};
	};
} {1};

#alias {/d} {
	#map get roomvnum markupsRoomVnum;
	#line sub variables {
		#class markups read markups.tt;
		#class markups open;
		#if {$m[$markupsRoomVnum][rendered] > 0} {
			#map set roomsymbol {$m[$markupsRoomVnum][symbol]};
			#map set roomterrain {$m[$markupsRoomVnum][terrain]};
			#unvar m[$markupsRoomVnum];
			#line ignore #sh {<178>Highlighter: <028>Usunalem.};
		} {
			#line ignore #sh {<178>Highlighter: <038>Pokoj nie byl podswietlony!};
		};

		#class markups close;
		#class markups write markups.tt;
	};
} {1};


#var showingmarkups 0;
#alias {/show} {
	#foreach {*m[]} {_m} {
		#if {"$m[$_m][type]" == "rot"} {
			#map at {$_m} {/d};
		};
	};
	#if {$showingmarkups < 1} {
		#foreach {*m[]} {_m} {
			#map at {$_m} {
				#var m[$_m][rendered] {1};
				#map set roomsymbol $markupsymbols[$m[$_m][type]];
			};
			#if {"$m[$_m][type]" == "ext"} {
				#map at {$_m} {#map get roomexits _rex;};
				#foreach {*m[$_m][direction][]} {mdir} {
					#switch {"$mdir"} {
						#case {"n"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[n]} {/e ext s}};
						#case {"s"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[s]} {/e ext n}};
						#case {"w"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[w]} {/e ext e}};
						#case {"e"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[e]} {/e ext w}};
						#case {"ne"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[ne]} {/e ext sw}};
						#case {"nw"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[nw]} {/e ext se}};
						#case {"sw"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[sw]} {/e ext ne}};
						#case {"se"} {#map at {$_m} {#map exit $mdir color <138>};#map at {$_rex[se]} {/e ext nw}};
					};
				};

			};
		};
		#var showingmarkups 1;
	};
} {1};

#alias {/destroy} {
	#foreach {*m[]} {_m} {
		#map at {$_m} {
			#if {"$m[$_m][type]" == "ext"} {
				#map exit $m[$_m][direction] color;
			};
		};
		#map at {$_m} {/d};
	};
} {1};

#action {%*% swiata zostalo opanowane przez Ciemnosc.} {
	#var tmpvalue %1;

	#class markups read markups.tt;
	#class markups open;
	#if {$apka} {
		#if {$tmpvalue < $apka} {
			#line ignore #showme {<138>Podswietlanie: <088>Baza pokojow jest przedawniona. Wpisz <118>/destroy<088> aby usunac stare wpisy.};
		} {
			#list m size msize;
			#line ignore #showme {<138>Podswietlanie: <088>Odswiezam stan apokalipsy. Zaladowane jest: <128>$msize<088> pokojow.};
			#var apka $tmpvalue;
			#unvar msize;
		}
	} {
		#list m size msize;
		#line ignore #showme {<138>Podswietlanie: <088>Dodaje stan apokalipsy. Zaladowane jest: <128>$msize<088> pokojow.};
		#var apka $tmpvalue;
		#unvar msize;
	};
	#class markups close;
	#class markups write markups.tt;
};

/show
