#nop VSOF;

#nop The bundle event handler;
#alias {_killstats_check_progress} {
	#var {CHAR_STATE} {%0};
	#foreach {*CHAR_STATE[]} {s} {
		#list {state} {find} {$s} {v};
		#if {$v > 0} {
			#list {state}{set}{$s}{$CHAR_STATE[$s]};
		} {
			#var {state[$s]} {$CHAR_STATE[$s]};
		};
	};

	#if {"$CHAR_STATE[improve]" != ""} {_killstats_add $CHAR_STATE[improve]};
};

#alias {globalnypostep} {
	#nop nothing to do;
};

#alias {staz} {
  #nop nothing to do;
};

#alias {/staz} {
    #math _staz_ {$ConfigManager_Configs[staz] * 100 / 240};
    #line ignore #showme {<fff>arkatt: <088>Staz $ConfigManager_Configs[staz] / 240 == $_staz_%};
    #unvar _staz_;
} {1};
#action {Twoja wysoka forma pozwala ci nieznacznie przyspieszyc nauke zawodu.} {
    #math tmppst {$ConfigManager_Configs[staz] + 1};
    /conf staz $tmppst;
    #unvar tmppst;
} {1};

#alias {celpostepow} {
	#if {"$ConfigManager_Configs[celpostepow]" == ""} {
		#var ConfigManager_Configs[celpostepow] 1200;
	};
	#nop nothing to do;
};


#function {poziomy_postepow}{
	#var postepy {minimalne};
	#switch {%1} {
		#case {1}  {#var postepy nieznaczne};
		#case {2}  {#var postepy bardzo male};
		#case {3}  {#var postepy male};
		#case {4}  {#var postepy nieduze};
		#case {5}  {#var postepy zadowalajace};
		#case {6}  {#var postepy spore};
		#case {7}  {#var postepy dosc duze};
		#case {8}  {#var postepy znaczne};
		#case {9}  {#var postepy duze};
		#case {10} {#var postepy bardzo duze};
		#case {11} {#var postepy ogromne};
		#case {12} {#var postepy imponujace};
		#case {13} {#var postepy wspaniale};
		#case {14} {#var postepy gigantyczne};
		#case {15} {#var postepy niebotyczne};
	};

	#return $postepy;
};

#nop ###############;
#nop # cache class #;
#nop ###############;

#alias {_killstats_save_cache}{
	#class {_killstats} kill;
	#class {_killstats} open;
		#var _killstats_cache $expo;
		#var _killstats_postep $current_postep;
	#class {_killstats} close;
	#class {_killstats} write stats-$AVATAR_NAME;
};

#alias {_killstats_load_cache}{
	#class {_killstats} open;
		#var _killstats_postep {666};
	#class {_killstats} close;

	#class {_killstats} read stats-$AVATAR_NAME;
	#if {$_killstats_postep == %1 } {
		#nop recovering;
		#var expo $_killstats_cache;
		#var kill_count {$expo[%1][ownkills]};
		#var they_kill_count {$expo[%1][theirkills]};
	} {
		_killstats_init_postep {%1};
		_killstats_save_cache;
	}
};

#nop Nie poczyniles zadnych postepow, od kiedy wszedles do gry;
#nop Poczyniles {minimalne|nieznaczne|bardzo male|male} postepy, od momentu kiedy wszedles do gry;

#nop expo[poziom][czas|ownkills|theirkills|totalkills];

#var pierwszy_postep {0};

#action {^Zabil{es|as} %2.$}
{
	#math {kill_count} {$kill_count+1};
	#math {total_kill_count} {$kill_count + $they_kill_count};

	#var expo[$current_postep][ownkills] {$kill_count};
	#var expo[$current_postep][totalkills] {$total_kill_count};

	_killstats_save_cache;

	#line ignore {#sh {<fca>%0 (Od @poziomy_postepow{$current_postep}: $kill_count/$total_kill_count)}};
	#nop echo {==> Zabici od @poziomy_postepow{$current_postep}: $kill_count/$total_kill_count};
	#line gag;
};

#action {%1 zabi{la|l} %3.$}
{
	#math {they_kill_count} {$they_kill_count+1};
	#math {total_kill_count} {$kill_count + $they_kill_count};

	#var expo[$current_postep][theirkills] {$they_kill_count};
	#var expo[$current_postep][totalkills] {$total_kill_count};

	_killstats_save_cache;
	#line ignore {#sh {<fca>%0 (Od @poziomy_postepow{$current_postep}: $kill_count/$total_kill_count)}};
	#nop echo {==> Zabici od @poziomy_postepow{$current_postep}: $kill_count/$total_kill_count};
	#line gag;
};

#alias {_killstats_init_postep}{
	#echo {_killstats_add starting postep %1};

	#format {_p_c_stamp_} {%t} {%H:%M};
	#format {_p_c_stamp_two_} {%T};

	#var expo[$current_postep][czas] {$_p_c_stamp_};
	#var expo[$current_postep][epock] {$_p_c_stamp_two_};
	#var expo[$current_postep][ownkills] {0};
	#var expo[$current_postep][theirkills] {0};
	#var expo[$current_postep][totalkills] {0};
	#var kill_count {0};
	#var they_kill_count {0};
};

#alias {_killstats_add} {
	#var current_postep %1;

	#if {$current_postep == 0} {
		#nop 06-Jun-21 Prompt default value;
		#var postep_needed {10};
	} {
		#math poprzedni_postep {$current_postep - 1};
		#var postep_needed $expo[$poprzedni_postep][totalkills];
	};
	#nop #echo {_killstats_add called with %1};
	#if {&{$expo[$current_postep][czas]} == 0} {
		#if {$pierwszy_postep == 0}{
			#var pierwszy_postep {1};
			_killstats_load_cache %1;
		} {
			_killstats_init_postep %1;
			/kills;
			#math _globalnypostep {$ConfigManager_Configs[globalnypostep] + 1};
			/conf globalnypostep $_globalnypostep;
		};
	} {
		#nop should never happen?;
		#echo {ERROR in KillStats bundle: $expo[$current_postep][czas] $current_postep};
	};
}

#alias /kills {
    #local _killscnt_ 0;

  #echo {+-----------------+-czas--+-minut-+-ja/dru-+};
  #foreach {*expo[]}{exp}{
  	#var postep {@poziomy_postepow{$exp}};

    #if {$_killscnt_ > 0} {
        #math _killsprvs_ {$_killscnt_ - 1};
        #math _minutes_since_ {($expo[$_killscnt_][epock] - $expo[$_killsprvs_][epock]) / 60};
        #if {$_minutes_since_ < 25} {#var _minutes_since_ {<caf>$_minutes_since_<088>}};
        #if {$_minutes_since_ < 35 && $_minutes_since_ > 24} {#var _minutes_since_ {<afa>$_minutes_since_<088>}};
        #if {$_minutes_since_ < 45 && $_minutes_since_ > 34} {#var _minutes_since_ {<ffa>$_minutes_since_<088>}};
        #if {$_minutes_since_ > 44} {#var _minutes_since_ {<faa>$_minutes_since_<088>}};
    } {
        #var _minutes_since_ {-};
    };

  	#format {line} {| %-15s | %-5s | %-5s | %-6s |} {$postep} {$expo[$exp][czas]} {$_minutes_since_} {$expo[$exp][ownkills]/$expo[$exp][totalkills]};

  	#echo {$line};
    #math _killscnt_ {$_killscnt_ + 1};
  };
  #echo {+-----------------+-------+-------+--------+};
  #math niebotow {$ConfigManager_Configs[globalnypostep] / 15};
  #format {line} {| Globalny:  %+4s | Niebotow           %+3s |} {$ConfigManager_Configs[globalnypostep]} {$niebotow};
  #echo {$line};
  #echo {+------------------------------------------+};
  #unvar niebotow;
}{1};
