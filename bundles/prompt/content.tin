#var global_bundle_slots_values[prompt][bottom] {1};
#var zmeczenie[0] {w pelni wypoczety};
#var zmeczenie[1] {wypoczety};
#var zmeczenie[2] {troche zmeczony};
#var zmeczenie[3] {zmeczony};
#var zmeczenie[4] {bardzo zmeczony};
#var zmeczenie[5] {nieco wyczerpany};
#var zmeczenie[6] {wyczerpany};
#var zmeczenie[7] {bardzo wyczerpany};
#var zmeczenie[8] {wycienczony};
#var zmeczenie[9] {calkowicie wycienczony};

#var state[fatigue] {0};
#var state[hp] {0};
#var state[mana] {0};

#var bbs_fixer 0;

#alias {meldujprzy} {
	#var pmeldujprzy $ConfigManager_Configs[meldujprzy];
};

#alias {pasek} {
	#if {"$ConfigManager_Configs[pasek]" == ""} {
		/conf pasek medium
	} {
		#switch {"$ConfigManager_Configs[pasek]"} {
			#case {"medium"} {
				#alias BottomBar_Selected {BottomBarTinyFugue};
			};
			#case {"mini"} {
				#alias BottomBar_Selected {BottomBarMini};
			};
			#case {"micro"} {
				#alias BottomBar_Selected {BottomBarMicro};
			};
			#case {"keyboard"} {
				#alias BottomBar_Selected {BottomKeyboard};
			};
		};
	};
	SplitRefreshRequestRefresh;
}

#alias {/zme} {
	#if {"$duch[$avatarid][team]" == "true"} {
		szepnij ob_$team_lead_id $zmeczenie[$charstate[fatigue]];
	} {
		#line ignore #sh {Nie jestes w zadnej druzynie.};
	};
} {1};

#alias {BottomBarSimple_char_state} {
	#var charstate {%0};
	#foreach {*charstate[]} {s} {
		#list {state} {find} {$s} {v}; 
		#if {$v > 0} {
			#list {state}{set}{$s}{$charstate[$s]}
		} {
			#var {state[$s]} {$charstate[$s]};
		}
	};
	#if {$charstate[fatigue] >= $pmeldujprzy} {
		#if {"$duch[$avatarid][team]" == "true"} {
			#line sub variables {
				#alias melduj_zmeczenie {
					szepnij ob_$team_lead_id $zmeczenie[$charstate[fatigue]];
				};
			};
			qb melduj_zmeczenie;
		};
	};
	BottomBar_Selected;
};

#nop #############################################################;
#nop ############# UKRYWANIE               #######################;
#nop #############################################################;
#var hiddenlevel {${c_stan_niski}off};
#action {Chowasz sie najlepiej jak potrafisz} {
	#var hiddenlevel {${c_stan_sredni} 0%};BottomBar_Selected;
	#delay {delayhidden1} {#var hiddenlevel {${c_stan_sredni}33%}} {5};
	#delay {delayhidden2} {#var hiddenlevel {${c_stan_sredni}66%}} {10};
	#delay {delayhidden3} {#var hiddenlevel {${c_stan_wysoki}on }} {15};
};
#action {Nie jestes juz schowany} {
	#undelay {delahidden%*};
	#var hiddenlevel {${c_stan_niski}off};
};


#nop #############################################################;
#nop ############# MANEWRY                 #######################;
#nop #############################################################;
#var manewrlevel {5};
#alias {manewr_start} {
	#var manewrlevel {0};BottomBar_Selected;
	#delay {manewrlevel5} {#var manewrlevel {5};BottomBar_Selected;} {5};
	#delay {manewrlevel4} {#var manewrlevel {4};BottomBar_Selected;} {4};
	#delay {manewrlevel3} {#var manewrlevel {3};BottomBar_Selected;} {3};
	#delay {manewrlevel2} {#var manewrlevel {2};BottomBar_Selected;} {2};
	#delay {manewrlevel1} {#var manewrlevel {1};BottomBar_Selected;} {1};
	
};
#nop #############################################################;
#nop ############# ROZKAZY                 #######################;
#nop #############################################################;
#var rozkazlevel {15};
#alias {rozkaz_start} {
	#var rozkazlevel {0};
	#delay {rozkazlevel15} {#var rozkazlevel {15};BottomBar_Selected;} {15};
	#delay {rozkazlevel14} {#var rozkazlevel {14};BottomBar_Selected;} {14};
	#delay {rozkazlevel13} {#var rozkazlevel {13};BottomBar_Selected;} {13};
	#delay {rozkazlevel12} {#var rozkazlevel {12};BottomBar_Selected;} {12};	
	#delay {rozkazlevel11} {#var rozkazlevel {11};BottomBar_Selected;} {11};
	#delay {rozkazlevel10} {#var rozkazlevel {10};BottomBar_Selected;} {10};
	#delay {rozkazlevel9} {#var rozkazlevel {9};BottomBar_Selected;} {9};
	#delay {rozkazlevel8} {#var rozkazlevel {8};BottomBar_Selected;} {8};
	#delay {rozkazlevel7} {#var rozkazlevel {7};BottomBar_Selected;} {7};
	#delay {rozkazlevel6} {#var rozkazlevel {6};BottomBar_Selected;} {6};
	#delay {rozkazlevel5} {#var rozkazlevel {5};BottomBar_Selected;} {5};
	#delay {rozkazlevel4} {#var rozkazlevel {4};BottomBar_Selected;} {4};
	#delay {rozkazlevel3} {#var rozkazlevel {3};BottomBar_Selected;} {3};
	#delay {rozkazlevel2} {#var rozkazlevel {2};BottomBar_Selected;} {2};
	#delay {rozkazlevel1} {#var rozkazlevel {1};BottomBar_Selected;} {1};
	BottomBar_Selected;
};
#nop #############################################################;
#nop ############# BLOKOWANIE TWOJE        #######################;
#nop #############################################################;
#var blockselflevel {0};
#alias {blok_start} {
	#var blockselflevel {0};
	#delay {blockselflevel5} {#var blockselflevel {5};BottomBar_Selected;} {5};
	#delay {blockselflevel4} {#var blockselflevel {4};BottomBar_Selected;} {4};
	#delay {blockselflevel3} {#var blockselflevel {3};BottomBar_Selected;} {3};
	#delay {blockselflevel2} {#var blockselflevel {2};BottomBar_Selected;} {2};
	#delay {blockselflevel1} {#var blockselflevel {1};BottomBar_Selected;} {1};
	BottomBar_Selected;
};



#nop ROZKAZY;
#action {Wydajesz rozkaz %1} {
	#line ignore #sh {<168>[  ROZKAZ  ] %0<088>};
	manewr_start;
	rozkaz_start;
	#line gag;
};
#action {Glosno wypowiadasz rozkaz, chyba jednak nikt cie nie zrozumial!} {
	#line ignore #sh {<bad>[ROZKAZ NIEUDANY] %0<088>};
	manewr_start;
	rozkaz_start;
	#line gag;
};
#nop MANEWRY;
#action {Twoja wysoka forma pozwala %*} {
	manewr_start;
};
#action {Na rozkaz %* zaslaniasz {go|ja} przed ciosami wrogow.} {
	#line ignore #sh {\n<acf>[  ROZKAZ ZASLONA  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Probujesz zaslonic %* przed ciosami %*, jednak nie jestes w stanie tego uczynic.} {
	#line ignore #sh {<bad>[NIE ZASLANIASZ] %0<088>};
	manewr_start;
	#line gag;
};
#action {Nie jestes jeszcze gotow do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<G02>}} {0.1};
	#line gag;
};
#action {Nie jestes jeszcze gotowa do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<G02>}} {0.1};#line gag;
};
#action {Nie zaslaniasz nikogo.} {
	#line gag;
};

#action {%* probuje zaslonic %* przed ciosami %*, jednak nie jest w stanie tego uczynic.} {
	#line ignore #sh {<bad>[NIE ZASLANIA] %0<088>};
	#line gag;
} {1};

#action {Zrecznie zaslaniasz %* przed ciosami %*.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Stajesz u boku %1, gotow w kazdej chwili %*} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Z wprawa stajesz pomiedzy %* a %*, przyjmujac na siebie nadchodzace ciosy.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
} {2}
#action {Przestajesz zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJESZ  ] %0<088>};
	#line gag;
}
#action {%* przestaje zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;	
}
#action {%* przestaje cie zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;	
}
#action {%1 unosi %2 i szybko przesuwa sie za ciebie, kryjac sie przed atakami %3.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Rzucasz sie na %* przebijajac sie przez %*} {
	#line ignore #sh {\n<fac>[  PRZELAMALES  ] %0<088>\n};
	manewr_start;
	#line gag;
};

#action {%* rzuca sie na %* przebijajac sie przez %*} {
	#format ktozaslonil {%l} {%1};
	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh {<fac>[  PRZELAMAL - POPRAW CEL  ] %0<088>};
				#var pokazalem 1;
			} {
				#nop line ignore #sh {<FAA><178>[ZASLONA] %0<088>};
				#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
				#var pokazalem 1;
			};
		};
	};
	#if {$pokazalem != 1} {
				#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
	};
	#line gag;
}


#action {%* rzuca sie na %* bezskutecznie %*} {
	#line ignore #sh {<bad>[NIE PRZELAMAL] %0<088>};
	#line gag;
};


#action {%* zrecznie zaslania %*} {
	#format ktozaslonil {%l} {%1};
	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
				#var pokazalem 1;
			} {
				#nop line ignore #sh {<FAA><178>[ZASLONA] %0<088>};
				#line ignore #sh {<fac>[  ZASLANIA - PRZELAM  ] %0<088>};
				#var pokazalem 1;
			};
		};
	};
	#if {$pokazalem != 1} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
	};
	#line gag;
}

#nop BLOCKOWANIE;
#nop action {Przymierzasz sie do odciecia} {blok_start};
#nop action {Zajmujesz pozycje umozliwiajaca odciecie};
#nop action {{Przerywasz przygotowania do odciecia|Przestajesz odcinac droge |Udaje ci sie ominac | omija twoj nieskuteczny |Uciekl{e|a}s|przestaje%* odcinac| podazasz}};

#nop #############################################################;
#nop ############# BLOKOWANIE W CIEBIE     #######################;
#nop #############################################################;
#action {przymierza sie do odciecia ci drogi ucieczki} {
	blok_start;
};
#action {zajmuje pozycje umozliwiajaca odciecie ci drogi ucieczki} {
	blok_start;
};
#action {przestaje odcinac twoja droge ucieczki} {
	#var blockselflevel 0;
	#if {"$ConfigManager_Configs[pasek]" == "medium"} {
		rysuj_paski;
	};
};


#nop #############################################################;
#nop ############# WARNINGS                #######################;
#nop #############################################################;
#nop action {{Powoli osuwasz sie na ziemie|Potem robi sie ciemno|Sila uderzania zamroczyla cie|czujesz, ze tracisz przytomnosc|Nagle czujesz jak na glowe spada ci ciezki sznur|Nagle czujesz, ze tracisz kontrole nad swym cialem| wali cie na odlew %* chwiejesz|ze swiat przed twoimi oczami niknie|Przymroczony tym uderzeniem czujesz jak nogi|Swiat przed twoimi oczami ciemnieje. Czujesz, ze tracisz wzrok!}} {};
#nop action {{Powoli dochodzisz do siebie|Czujesz jak slabosc po zadanym ciosie w glowe mija|Udaje ci sie uwolnic z sieci|Powoli odzyskujesz swobode ruchow}} {};


#nop #############################################################;
#nop ############# WEAPONS                 #######################;
#nop #############################################################;
#var bronstatus off;
#action {{Juz dzierzysz|Gwaltownym ruchem wyszarpujesz|Dobywasz|Wiedziony naglym instynktem siegasz|Pelnym pewnosci siebie ruchem dobywasz| twego pewnego chwytu, | zaciskasz dlon |Trzymasz|dobywajac}}
{
	#var bronstatus on;
};
#action {{Opuszczasz | ogarniajac wzrokiem cala okolice| opuszczasz | rozluzniasz uchwyt | do tej pory twe serce }%*.}
{
	#line ignore #showme {<ffa>[bron]<088> %0};
	#var bronstatus off;
	#line gag;
};
#action {{wytraca ci|Uderzenie jest tak silne, ze bezwiednie puszczasz|Czujesz potezne szarpniecie|Bezwladnie opuszczasz}%*} {
	#line ignore #showme {<ffa>[bron]<088> %0};
	#var bronstatus off;
	#line gag;
};
#action {Opuszczasz obszar} {
	#nop;
} {1};

#action {Ciemne miejsce} {
	#var rozk2 {{w}{ }{e}{ }{n}{ }{s}{ }{nw}{ }{ne}{ }{se}{  }{sw}{  }{u}{ }{d}{ }{wyjscie}{   }{nw2}{ }{n2}{   }{ne2}{   }{w2}{ }{e2}{ }{sw2}{   }{s2}{   }{se3}{   }};
	#if {"$ConfigManager_Configs[pasek]"} {
		rysuj_roze;
	};
};

#nop #############################################################;
#nop #############################################################;
#nop #############################################################;



#var qbvar {                                                                 };


#var bbBGC {<G02>};
#event {VARIABLE UPDATED bbBGC} {
	BottomBar_Selected;	
};
#event {VARIABLE UPDATED charstate} {
	BottomBar_Selected;	
};

#alias {BottomBar_Selected} {
	#var global_bundle_slots_values[prompt][bottom] {1};
};

#alias {BottomBarMini} {
	#var global_bundle_slots_values[prompt][bottom] {3};
	#line ignore #sh {${bbBGC}HP:$state[hp]/6 MN:$state[mana]/8 ST:$state[fatigue]/9 | RO:$rozkazlevel/15 MA:$manewrlevel/5<088>} {-3} {2};
}

#var microhp[0] {<FAA><178>*<088>------};
#var microhp[1] {<118>**<088>-----};
#var microhp[2] {<138>***<088>----};
#var microhp[3] {<138>****<088>---};
#var microhp[4] {<128>*****<088>--};
#var microhp[5] {<128>******<088>-};
#var microhp[6] {<128>*******<088>};


#alias {BottomKeyboard} {
	#var global_bundle_slots_values[prompt][bottom] {14};
    #if {"$rozk2[nw]" != " "} {#var blockcolor[nw] <ABA>;} {#var blockcolor[nw] <G03>;};
    #if {"$rozk2[ne]" == "/"} {#var blockcolor[ne] <ABA>;} {#var blockcolor[ne] <G03>;};
    #if {"$rozk2[n]" == "|"} {#var blockcolor[n] <ABA>;} {#var blockcolor[n] <G03>;};
    #if {"$rozk2[w]" == "-"} {#var blockcolor[w] <ABA>;} {#var blockcolor[w] <G03>;};
    #if {"$rozk2[e]" == "-"} {#var blockcolor[e] <ABA>;} {#var blockcolor[e] <G03>;};
    #if {"$rozk2[s]" != " "} {#var blockcolor[s] <ABA>;} {#var blockcolor[s] <G03>;};
    #if {"$rozk2[se]" != "  "} {#var blockcolor[se] <ABA>;} {#var blockcolor[se] <G03>;};
    #if {"$rozk2[sw]" != "  "} {#var blockcolor[sw] <ABA>;} {#var blockcolor[sw] <G03>;};
    #if {"$rozk2[d]" != " "} {#var blockcolor[d] <ABA>;} {#var blockcolor[d] <G03>;};
    #if {"$rozk2[u]" != " "} {#var blockcolor[u] <ABA>;} {#var blockcolor[u] <G03>;};
    #if {"$rooa" != ""} {#var blockcolor[bind] <AAB><fff>} {#var blockcolor[bind] <G01><g01>};

    #screen clear square $screenrows-13 1 $screenrows $screencols;

    #draw <088><g08> rounded table $screenrows-13 1 $screenrows $screencols {<178>q;w;e;r;t;y;u;i;o;p;$blockcolor[nw]NW\n;$blockcolor[n]N\n;$blockcolor[ne]NE\n;$blockcolor[u]A\n} {;a;s;d;f;g;h;j;k;l;A;$blockcolor[w]W\n;$blockcolor[bind]LOK\n;$blockcolor[e]E\n;<FFA><aaa>BIND\n} {z;x;c;v;b;n;m;A;B;C;$blockcolor[sw]SW\n;$blockcolor[s]S\n;$blockcolor[se]SE\n;$blockcolor[d]V\n};
    #format microbarline {%h} {  $microhp[$state[hp]]  ## MN:$state[mana]/8 ST:$state[fatigue]/9 ## RO:$rozkazlevel/15 MA:$manewrlevel/5<088>  };
	#line ignore #sh {$microbarline'} {-15};
}
#alias {BottomBarMicro} {
	#var global_bundle_slots_values[prompt][bottom] {1};
	#format microbarline {%h} {  $microhp[$state[hp]]  ## MN:$state[mana]/8 ST:$state[fatigue]/9 ## RO:$rozkazlevel/15 MA:$manewrlevel/5<088>  };
	#line ignore #sh {$microbarline'} {-2};
};

#alias {BottomBarTinyFugue} {
	#var global_bundle_slots_values[prompt][bottom] {3};
	#draw {$bbBGC} tile $screenrows-3 1 $screenrows-1 $screencols-1;
	#nop RYSUJEMY ROZE PO LEWEJ;
	rysuj_roze; 
	#nop RYSUJEMY PASEK FORMY;
	rysuj_forme;
	#nop RYSUJEMY CUSTOM EXITS;
	rysuj_customs;
	#nop RYSUJEMY PASKI GLOWNE;
	rysuj_paski;
	#nop RYSUJEMY PRZYCISKI WALKI;
	rysuj_przyciski;
};

#alias rysuj_forme {
	#math {pozycjapaskaformy} {$screencols - 16};
	#line ignore #sh {${bbBGC}Forma : $state[form] Glod: $state[stuffed]<088>} {-4} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}Panika: $state[panic] Prag: $state[soaked]<088>} {-3} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}Ciezar: $state[encumbrance] Pija: $state[intox]<088>} {-2} {$pozycjapaskaformy};
}
#var ptrybchodzeniasymbol {<128>W};
#alias rysuj_roze {
	#line ignore #sh {$bbBGC   <138>$rozk2[nw] $rozk2[n] $rozk2[ne] $rozk2[u] <088>} {-4} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[w]$rozk2[w] $ptrybchodzeniasymbol <138>$rozk2[e]$rozk2[e]  <088>} {-3} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[sw] $rozk2[s] $rozk2[se]$rozk2[d] <088>} {-2} {1};
};

#alias rysuj_przyciski {
	#math {pozycjaprzyciskow} {$screencols - 42};
	#line ignore #sh {${bbBGC}Atak    <088>$buttons[0][8][label]${bbBGC} <088>$buttons[0][7][label]${bbBGC} <088>$buttons[0][6][label]${bbBGC} <088>$buttons[0][5][label]${bbBGC}<088>} {-4} {$pozycjaprzyciskow};
	#line ignore #sh {${bbBGC}Zaslona <088>$buttons[1][8][label]${bbBGC} <088>$buttons[1][7][label]${bbBGC} <088>$buttons[1][6][label]${bbBGC} <088>$buttons[1][5][label]${bbBGC}<088>} {-3} {$pozycjaprzyciskow};
	#line ignore #sh {${bbBGC}Reszta  <088>$buttons[2][8][label]${bbBGC} <088>LAM${bbBGC} <088>PLA${bbBGC}    <088>} {-2} {$pozycjaprzyciskow};	
};

#alias rysuj_customs {
	#draw {<088>$bbBGC} tile $screenrows-3 12 $screenrows-1 22 $bbBGC<ffa>$roza_custom;
};
#alias rysuj_paski {
	#math remainingArea {$screencols - (16 + 10 + 12)};
	#math maxBarSize {($remainingArea / 3) - 10};
	#math realFatigue {9 - $state[fatigue]};
	#sh {${bbBGC}HP: [@buildBar{$state[hp];6;$maxBarSize;ada}<088>${bbBGC}]  MA: [@buildBar{$manewrlevel;5;$maxBarSize;ccc}<088>${bbBGC}]<088>} {-4} {23};
	#sh {${bbBGC}ST: [@buildBar{$realFatigue;9;$maxBarSize;ddd}<088>${bbBGC}]  RO: [@buildBar{$rozkazlevel;15;$maxBarSize;ccc}<088>${bbBGC}]<088>} {-3} {23};
	#sh {${bbBGC}MA: [@buildBar{$state[mana];8;$maxBarSize;aad}<088>${bbBGC}]  BL: [@buildBar{$blockselflevel;5;$maxBarSize;faa}<088>${bbBGC}]<088>} {-2} {23};
};

#nop 1. current state;
#nop 2. max state;
#nop 3. max width;
#nop 4. colour;
#function buildBar {
	#local outputbar {};

	#if {%1 > 0} {
		#local curst {%1};
		#math stslot {%3 / %2}; 
		#math stmodu {%3 % %2};
		#math stlen {($curst * $stslot) + $stmodu};
		#loop {1} {$stlen-1} {stl} {#local outputbar {$outputbar#};};
		#loop {$stlen} {%3} {stl}  {#local outputbar {$outputbar };};
	} {
		#loop {0} {%3-1} {stl} {#local outputbar {$outputbar };};
	};
	#if {%1 == %2} {
		#local outputbar {};
		#loop {1} {%3} {stl} {#local outputbar {$outputbar#};};
	};

	#return {<%4>$outputbar<088>};
};

#alias prompt_pressed_mouse_button_one {
    #screen get ROWS sr;
    #screen get SPLIT_BOT_BAR sb;
    #math topButtonsFrameBorder {$sr - $sb};
    #nop var topButtonsFrameBorder 1;
    #nop if {%1 >= $topButtonsFrameBorder && %1 < $topButtonsFrameBorder+5 && %2 > 1 && %2 < $screencols} {};
        #math currRow {%1 - ($topButtonsFrameBorder)};
        #math currCol {($screencols - %2 + 1) / 4};

        #if {$mousepresseddebug} {
        	#line ignore #sh {<028>Mouse> <128>Button Click [$currRow] [$currCol]};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "wskazywanie"} {
            #local tmpflagi $pwskazywanie;
            #replace tmpflagi {A} {};
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                #replace tmpflagi {$buttons[$currRow][$currCol][value]} {};
            } {
                #local tmpflagi {${tmpflagi}$buttons[$currRow][$currCol][value]};
            };
            /conf atakflagi A${tmpflagi};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "zaslanianie"} {
            #local tmpflagi $pzaslonaflagi;
            #replace tmpflagi {A} {};
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                #replace tmpflagi {$buttons[$currRow][$currCol][value]} {};
            } {
                #local tmpflagi {${tmpflagi}$buttons[$currRow][$currCol][value]};
            };
            /conf zaslonaflagi A${tmpflagi};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "switch"} {
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                $buttons[$currRow][$currCol][cmdoff];
            } {
                $buttons[$currRow][$currCol][cmdon];
            }
        };
};


#event {VARIABLE UPDATED pwskazywanie} {
    #regex {$pwskazywanie} {z} {
        #var buttons[0][8][label] {<BAA><fff>ZAB<088>};
        #var buttons[0][8][status] {on};
    } {
        #var buttons[0][8][label] {ZAB};
        #var buttons[0][8][status] {off};
    };
    #regex {$pwskazywanie} {w} {
        #var buttons[0][7][label] {<BAB><fff>WSK<088>};
        #var buttons[0][7][status] {on};
    } {
        #var buttons[0][7][label] {WSK};
        #var buttons[0][7][status] {off};
    };
    #regex {$pwskazywanie} {r} {
        #var buttons[0][6][label] {<ABB><fff>ROZ<088>};
        #var buttons[0][6][status] {on};
    } {
        #var buttons[0][6][label] {ROZ};
        #var buttons[0][6][status] {off};
    };
    #regex {$pwskazywanie} {p} {
        #var buttons[0][5][label] {<FAA><fff>PRZ<088>};
        #var buttons[0][5][status] {on};
    } {
        #var buttons[0][5][label] {PRZ};
        #var buttons[0][5][status] {off};
    };    
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
};

#event {VARIABLE UPDATED pzaslonaflagi} {
    #regex {$pzaslonaflagi} {z} {
        #var buttons[1][8][label] {<AAB><fff>ZAS<088>};
        #var buttons[1][8][status] {on};
    } {
        #var buttons[1][8][label] {ZAS};
        #var buttons[1][8][status] {off};
    };
    #regex {$pzaslonaflagi} {w} {
        #var buttons[1][7][label] {<BAB><fff>WSK<088>};
        #var buttons[1][7][status] {on};
    } {
        #var buttons[1][7][label] {WSK};
        #var buttons[1][7][status] {off};
    };
    #regex {$pzaslonaflagi} {r} {
        #var buttons[1][6][label] {<ABB><fff>ROZ<088>};
        #var buttons[1][6][status] {on};
    } {
        #var buttons[1][6][label] {ROZ};
        #var buttons[1][6][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
};

#event {VARIABLE UPDATED bronstatus} {
    #if {"$bronstatus" == "on"} {
        #var buttons[2][8][label] {<FAA><fff>BRO<088>};
        #var buttons[2][8][status] {on};
    } {
        #var buttons[2][8][label] {BRO};
        #var buttons[2][8][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
};

#event {VARIABLE UPDATED pzdjeciezaslon} {
    #if {"$pzdjeciezaslon" == "auto"} {
        #var buttons[1][5][label] {<AAB><fff>ZDE<088>};
        #var buttons[1][5][status] {on};
    } {
        #var buttons[1][5][label] {ZDE};
        #var buttons[1][5][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
}

#event {VARIABLE UPDATED ptrybchodzenia} {
	#switch {$ptrybchodzenia} {
		#case {0} {#var ptrybchodzeniasymbol <128>W};
		#case {1} {#var ptrybchodzeniasymbol <138>P};
		#case {2} {#var ptrybchodzeniasymbol <118>K};
	};
	#if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_roze;
    };

}