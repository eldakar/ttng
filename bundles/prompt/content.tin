#nop ###########################################################;
#nop Globalne ustawienia;
#nop ###########################################################;
#var global_bundle_slots_values[prompt][bottom] {1};
#var state[fatigue] {0};
#var state[hp] {0};
#var state[mana] {0};
#var bbs_fixer 0;
#var PONGED 0;

#nop CONF;	#alias {stylpaskow} {
				#if {"$ConfigManager_Configs[stylpaskow]" == "graph"} {
					#alias rysuj_paski rysuj_paski_graph;
				} {
					#alias rysuj_paski rysuj_paski_standard;
				};
				#if {"$ConfigManager_Configs[pasek]" == "medium"} {
					#draw <AAA> tile $screenrows-3 1 $screenrows-1 $screencols-1;
					BottomBar_Selected;
				};
			}; #alias rysuj_paski {#nop {};};

#nop CONF;	#alias {czatwpasku} {
				#if {"$ConfigManager_Configs[czatwpasku]" == "wlacz"} {
					#alias rysuj_czat rysuj_czat_wlacz;
				} {
					#alias rysuj_czat {#nop {};};
				};
				#if {"$ConfigManager_Configs[pasek]" == "medium"} {
					#draw <AAA> tile $screenrows-3 1 $screenrows-1 $screencols-1;
					BottomBar_Selected;
				};
			}; #alias rysuj_czat {#nop {};};

			#alias rysuj_czat_wlacz {
				#if {"$ConfigManager_Configs[czatwpasku]" == "wlacz" && "$ConfigManager_Configs[pasek]" == "medium"} {
					#if {"$ConfigManager_Configs[stylpaskow]" == "graph"} {
						#var dlugosc_paskow 83;
					} {
						#math dlugosc_paskow {$maxBarSize * 2 + 15};
					};
					#draw tile $screenrows-3 24+$dlugosc_paskow $screenrows-1 $screencols-22 $topchat[48]\n$topchat[49]\n$topchat[50];
					#nop topchat[$bcvc];
				};
			};

			#event {VARIABLE UPDATED chat_updated} {BottomBar_Selected;};


#nop ###########################################################;
#nop Naroznik: wartosc fps lub standard                         ;
#nop ###########################################################;
#nop CONF;	#alias {naroznikpaska} {
				#if {"$ConfigManager_Configs[naroznikpaska]" == "fps"} {
					#ticker fpsticker {
						#line quiet #line capture fpstest #line bench {#sh dupa};
						#regex {$fpstest[2]} {%* %d USEC.} {
							#math fpsTestResultValue {2000 - &2};
						};
					} {10};
					#ticker pingticker {
						#line quiet /ping;
					} {2};
					#alias rysuj_forme rysuj_forme_fps;
				} {
					#alias rysuj_forme rysuj_forme_standard;
					#untick pingticker;
					#untick fpsticker;
				};
			}; #alias rysuj_forme {#nop {};};
			#event {VARIABLE UPDATED fpsTestResultValue} {
				BottomBar_Selected;
			};
			#event {VARIABLE UPDATED _ping_} {
				BottomBar_Selected;
			};

			#event {VARIABLE UPDATED PONGED} {
				#switch {"$PONGED"} {
					#case {"1"} {#var mapPongedValue {<CDA><aaa> readonly }};
					#case {"2"} {#var mapPongedValue {<AFA><aaa>  online  }};
					#default {#var mapPongedValue    {<088> off-line }};
				};
				BottomBar_Selected;
			};

#nop ###########################################################;
#nop ###########################################################;


#alias {szerokoschpkow} {
	#if {"$ConfigManager_Configs[szerokoschpkow]" == ""} {
		#var pmaxbarsize auto;
	} {
		#var pmaxbarsize $ConfigManager_Configs[szerokoschpkow];
	};
	#if {"$ConfigManager_Configs[pasek]" == "medium" && "$ConfigManager_Configs[stylpaskow]" == "standard"} {
		#draw <AAA> tile $screenrows-3 1 $screenrows-1 $screencols-1;
		BottomBar_Selected;
	};
};

#alias {probymanewrow} {
	#if {"$ConfigManager_Configs[probymanewrow]" == "niewidoczne"} {
		#var pprobymanewrow 0;
	} {
		#var pprobymanewrow 1;
	};
}

#nop CONF;	#alias {pasek} {
				#if {"$ConfigManager_Configs[pasek]" == ""} {
					/conf pasek medium;
				} {
					#switch {"$ConfigManager_Configs[pasek]"} {
						#case {"medium"} {
							#alias BottomBar_Selected {BottomBarTinyFugue};
							#var global_bundle_slots_values[prompt][bottom] {3};
						};
						#case {"mini"} {
							#alias BottomBar_Selected {BottomBarMini};
							#var global_bundle_slots_values[prompt][bottom] {3};
						};
						#case {"micro"} {
							#alias BottomBar_Selected {BottomBarMicro};
							#var global_bundle_slots_values[prompt][bottom] {3};
						};
						#case {"keyboard"} {
							#alias BottomBar_Selected {BottomKeyboard};
							#var global_bundle_slots_values[prompt][bottom] {14};
						};
					};
				};
				#if {"$ConfigManager_Configs[pasek]" == "medium"} {
					#draw {$bbBGC} tile $screenrows-3 1 $screenrows-1 $screencols-1;
				};
				SplitRefreshRequestRefresh;
			}

#alias kolorzaslonwrogow {
	#if {"$ConfigManager_Configs[kolorzaslonwrogow]" == ""} {
		#var pkolorzaslonwrogow fac;
	} {
		#var pkolorzaslonwrogow $ConfigManager_Configs[kolorzaslonwrogow];
	};
	#line sub variables {#var colcovers {<$pkolorzaslonwrogow>}};
};
#nop EVENT;	#alias {BottomBarSimple_char_state} {
				        #local charstate {%0};
        				#foreach {*charstate[]} {s} {
    		    		    #list {state} {find} {$s} {v};
        		    		#if {$v > 0} {
            						#list {state}{set}{$s}{$charstate[$s]}
				            } {
        		    				#var {state[$s]} {$charstate[$s]};
					          };
    				    };
        				BottomBar_Selected;
			      };

#nop #############################################################;
#nop ############# UKRYWANIE               #######################;
#nop #############################################################;
#var hiddenlevel {0};
#action {Chowasz sie najlepiej jak potrafisz} {
	#var hiddenlevel {1};
	#delay {delayhidden1} {#var hiddenlevel {2}} {5};
	#delay {delayhidden2} {#var hiddenlevel {3}} {10};
	#delay {delayhidden3} {#var hiddenlevel {4}} {15};
};
#action {Nie jestes juz schowany} {
	#undelay {delahidden%*};
	#var hiddenlevel {0};
};


#nop #############################################################;
#nop ############# MANEWRY                 #######################;
#nop #############################################################;
#var manewrlevel {5};
#alias {manewr_start} {
	#var manewrlevel {0};BottomBar_Selected;
	#delay {manewrlevel5} {#var manewrlevel {5};BottomBar_Selected;} {5};
	#delay {manewrlevel4} {#var manewrlevel {4};BottomBar_Selected;} {4};
	#delay {manewrlevel3} {#var manewrlevel {3};BottomBar_Selected;} {3};
	#delay {manewrlevel2} {#var manewrlevel {2};BottomBar_Selected;} {2};
	#delay {manewrlevel1} {#var manewrlevel {1};BottomBar_Selected;} {1};
};
#nop #############################################################;
#nop ############# ROZKAZY                 #######################;
#nop #############################################################;
#var rozkazlevel {15};
#alias {rozkaz_start} {
	#var rozkazlevel {0};BottomBar_Selected;
	#delay {rozkazlevel15} {#var rozkazlevel {15};BottomBar_Selected;} {15};
	#delay {rozkazlevel14} {#var rozkazlevel {14};BottomBar_Selected;} {14};
	#delay {rozkazlevel13} {#var rozkazlevel {13};BottomBar_Selected;} {13};
	#delay {rozkazlevel12} {#var rozkazlevel {12};BottomBar_Selected;} {12};
	#delay {rozkazlevel11} {#var rozkazlevel {11};BottomBar_Selected;} {11};
	#delay {rozkazlevel10} {#var rozkazlevel {10};BottomBar_Selected;} {10};
	#delay {rozkazlevel9} {#var rozkazlevel {9};BottomBar_Selected;} {9};
	#delay {rozkazlevel8} {#var rozkazlevel {8};BottomBar_Selected;} {8};
	#delay {rozkazlevel7} {#var rozkazlevel {7};BottomBar_Selected;} {7};
	#delay {rozkazlevel6} {#var rozkazlevel {6};BottomBar_Selected;} {6};
	#delay {rozkazlevel5} {#var rozkazlevel {5};BottomBar_Selected;} {5};
	#delay {rozkazlevel4} {#var rozkazlevel {4};BottomBar_Selected;} {4};
	#delay {rozkazlevel3} {#var rozkazlevel {3};BottomBar_Selected;} {3};
	#delay {rozkazlevel2} {#var rozkazlevel {2};BottomBar_Selected;} {2};
	#delay {rozkazlevel1} {#var rozkazlevel {1};BottomBar_Selected;} {1};
};
#nop #############################################################;
#nop ############# BLOKOWANIE TWOJE        #######################;
#nop #############################################################;
#var blockselflevel {0};
#alias {blok_start} {
	#var blockselflevel {0};BottomBar_Selected;
	#delay {blockselflevel5} {#var blockselflevel {5};BottomBar_Selected;} {5};
	#delay {blockselflevel4} {#var blockselflevel {4};BottomBar_Selected;} {4};
	#delay {blockselflevel3} {#var blockselflevel {3};BottomBar_Selected;} {3};
	#delay {blockselflevel2} {#var blockselflevel {2};BottomBar_Selected;} {2};
	#delay {blockselflevel1} {#var blockselflevel {1};BottomBar_Selected;} {1};
};



#nop ROZKAZY;
#action {Wydajesz rozkaz %1} {
	#line ignore #sh {<168>[  ROZKAZ  ] %0<088>};
	manewr_start;
	rozkaz_start;
	#line gag;
};
#action {Glosno wypowiadasz rozkaz, chyba jednak nikt cie nie zrozumial!} {
	#line ignore #sh {<bad>[ROZKAZ NIEUDANY] %0<088>};
	manewr_start;
	rozkaz_start;
	#line gag;
};
#nop MANEWRY;
#action {Twoja wysoka forma pozwala %*} {
	manewr_start;
};
#action {Na rozkaz %* zaslaniasz {go|ja} przed ciosami wrogow.} {
	#line ignore #sh {\n<acf>[  ROZKAZ ZASLONA  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Probujesz zaslonic %* przed ciosami %*, jednak nie jestes w stanie tego uczynic.} {
		#line ignore #sh {<bad>[NIE ZASLANIASZ] %0<088>};
	manewr_start;
	#line gag;
};
#action {Nie jestes jeszcze gotow do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<088>}} {0.1};
	#line gag;
};
#action {Nie jestes jeszcze gotowa do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<088>}} {0.1};#line gag;
};
#action {Nie zaslaniasz nikogo.} {
	#line gag;
};

#action {%* probuje zaslonic %* przed ciosami %*, jednak nie jest w stanie tego uczynic.} {
	#if {$pprobymanewrow == 1} {
		#line ignore #sh {<bad>[NIE ZASLANIA] %0<088>};
	};
	#line gag;
} {1};

#action {Zrecznie zaslaniasz %* przed ciosami %*.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Stajesz u boku %1, gotow w kazdej chwili %*} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Z wprawa stajesz pomiedzy %* a %*, przyjmujac na siebie nadchodzace ciosy.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
} {2}
#action {Przestajesz zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJESZ  ] %0<088>};
	#line gag;
}
#action {%* przestaje zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;
}
#action {%* przestaje cie zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;
}
#action {%1 unosi %2 i szybko przesuwa sie za ciebie, kryjac sie przed atakami %3.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Rzucasz sie na %* przebijajac sie przez %*} {
	#foreach {*duch[]} {ktoto} {
		#if {@match{%1;$duch[$ktoto][desc]} == 0} {
			#var manewry[$ktoto] {przelamany};
			#line sub variables {
				#delay {3} {
					#unvar manewry[$ktoto];
					tabela_odswiez;
				    render_h2;
				    #var objectnumsRefreshed {1};
				};
			};
		};
	};
    tabela_odswiez;
    render_h2;
	#var objectnumsRefreshed {1};

	#line ignore #sh {\n<fac>[  PRZELAMALES  ] %0<088>\n};
	manewr_start;
	#line gag;
};

#action {%* rzuca sie na %* przebijajac sie przez %*} {
	#var ktozaslonil {%1};
	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {@match{%2;$duch[$ktoto][desc]} == 0} {
			#var manewry[$ktoto] {przelamany};
			#line sub variables {
				#delay {3} {
					#unvar manewry[$ktoto];
					tabela_odswiez;
				    render_h2;
				    #var objectnumsRefreshed {1};
				};
			};
		};
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh { };
				Highlights_event <118> $ktozaslonil PRZELAMAL - POPRAW CEL <088>;
				#line ignore #sh { };
				#var pokazalem 1;
			} {
				#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
				#var pokazalem 1;
			};
		};
	};
    tabela_odswiez;
    render_h2;
	#var objectnumsRefreshed {1};

	#if {$pokazalem != 1} {
		#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
	};
	#line gag;
}


#action {%* rzuca sie na %* bezskutecznie %*} {
	#if {$pprobymanewrow == 1} {
		#line ignore #sh {<bad>[NIE PRZELAMAL] %0<088>};
	};
	#line gag;
};

#nop MC wskazanie na bloka;
#action {%* mruzac oczy z sykiem wciaga powietrze %* wyprostowuje reke i msciwym gestem wskazuje na %*.} {
	#foreach {*duch[]} {ktoto} {
		#if {@match{%3;$duch[$ktoto][desc]} == 0} {
        #line sub variables {#alias zablokuj_go {zablokuj ob_$ktoto}};
        qm zablokuj_go;
		};
	};
};


#action {%* zrecznie zaslania %*} {
	#regexp {%1} {%w %w %w} {
		#format ktozaslonil {%l} {%1};
	} {
		#var ktozaslonil {%1};
	};

	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
				#var pokazalem 1;
			} {
				#line ignore #sh {${colcovers}[  ZASLANIA - PRZELAM  ] %0<088>};
				#var pokazalem 1;
			};
		};
	};
	#if {$pokazalem != 1} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
	};
	#line gag;
}

#nop BLOCKOWANIE;
#nop action {Przymierzasz sie do odciecia} {blok_start};
#nop action {Zajmujesz pozycje umozliwiajaca odciecie};
#nop action {{Przerywasz przygotowania do odciecia|Przestajesz odcinac droge |Udaje ci sie ominac | omija twoj nieskuteczny |Uciekl{e|a}s|przestaje%* odcinac| podazasz}};

#nop #############################################################;
#nop ############# BLOKOWANIE W CIEBIE     #######################;
#nop #############################################################;
#action {przymierza sie do odciecia ci drogi ucieczki} {
	blok_start;
};
#action {zajmuje pozycje umozliwiajaca odciecie ci drogi ucieczki} {
	blok_start;
};
#action {przestaje odcinac twoja droge ucieczki} {
	#var blockselflevel 0;
	BottomBar_Selected;
};


#nop #############################################################;
#nop ############# WARNINGS                #######################;
#nop #############################################################;
#nop action {{Powoli osuwasz sie na ziemie|Potem robi sie ciemno|Sila uderzania zamroczyla cie|czujesz, ze tracisz przytomnosc|Nagle czujesz jak na glowe spada ci ciezki sznur|Nagle czujesz, ze tracisz kontrole nad swym cialem| wali cie na odlew %* chwiejesz|ze swiat przed twoimi oczami niknie|Przymroczony tym uderzeniem czujesz jak nogi|Swiat przed twoimi oczami ciemnieje. Czujesz, ze tracisz wzrok!}} {};
#nop action {{Powoli dochodzisz do siebie|Czujesz jak slabosc po zadanym ciosie w glowe mija|Udaje ci sie uwolnic z sieci|Powoli odzyskujesz swobode ruchow}} {};

#nop #############################################################;
#nop ############# WEAPONS                 #######################;
#nop #############################################################;
#var bronstatus off;
#action {{Juz dzierzysz|Trzymasz}}
{
	#if {"$bronstatus" == "off"} {#var bronstatus on};
};
#action {{Gwaltownym ruchem wyszarpujesz|Dobywasz|Wiedziony naglym instynktem siegasz|Pelnym pewnosci siebie ruchem dobywasz| twego pewnego chwytu, | zaciskasz dlon |dobywajac}}
{
	#if {"$bronstatus" == "off"} {#var bronstatus on};
	#map update;
};
#action {{Opuszczasz | ogarniajac wzrokiem cala okolice| opuszczasz | rozluzniasz uchwyt | do tej pory twe serce }%*.}
{
	#line ignore #showme {<ffa>[bron]<088> %0};
	#if {"$bronstatus" == "on"} {#var bronstatus off};
	#line gag;
};
#action {{wytraca ci|Uderzenie jest tak silne, ze bezwiednie puszczasz|Czujesz potezne szarpniecie|Bezwladnie opuszczasz}%*} {
	#line ignore #showme {<ffa>[bron]<088> %0};
	#if {"$bronstatus" == "on"} {#var bronstatus off};
	#line gag;
};
#action {Opuszczasz obszar} {
	#nop;
} {1};



#nop #############################################################;
#nop #############################################################;
#nop #############################################################;



#var qbvar {                                                                 };


#var bbBGC {<088>};
#event {VARIABLE UPDATED bbBGC} {
	BottomBar_Selected;
};

#alias {BottomBar_Selected} {
	#var global_bundle_slots_values[prompt][bottom] {1};
};

#var minihphack[0] {<faa>1<088>};
#var minihphack[1] {<faa>2<088>};
#var minihphack[2] {<faa>3<088>};
#var minihphack[3] {<ffa>4<088>};
#var minihphack[4] {<ffa>5<088>};
#var minihphack[5] {<afa>6<088>};
#var minihphack[6] {<afa>7<088>};


#alias {BottomBarMini} {
	#line ignore #sh {HP:$minihphack[$state[hp]]/7 MN:$state[mana]/8 ST:$state[fatigue]/9 | RO:$rozkazlevel/15 MA:$manewrlevel/5<088>} {-3} {2};
}

#var microhp[0] {<FAA><178>*<088>------};
#var microhp[1] {<118>**<088>-----};
#var microhp[2] {<138>***<088>----};
#var microhp[3] {<138>****<088>---};
#var microhp[4] {<128>*****<088>--};
#var microhp[5] {<128>******<088>-};
#var microhp[6] {<128>*******<088>};


#alias {BottomKeyboard} {
    #if {"$rozk2[nw]" != " "} {#var blockcolor[nw] <ABA>;} {#var blockcolor[nw] <G03>;};
    #if {"$rozk2[ne]" == "/"} {#var blockcolor[ne] <ABA>;} {#var blockcolor[ne] <G03>;};
    #if {"$rozk2[n]" == "|"} {#var blockcolor[n] <ABA>;} {#var blockcolor[n] <G03>;};
    #if {"$rozk2[w]" == "-"} {#var blockcolor[w] <ABA>;} {#var blockcolor[w] <G03>;};
    #if {"$rozk2[e]" == "-"} {#var blockcolor[e] <ABA>;} {#var blockcolor[e] <G03>;};
    #if {"$rozk2[s]" != " "} {#var blockcolor[s] <ABA>;} {#var blockcolor[s] <G03>;};
    #if {"$rozk2[se]" != "  "} {#var blockcolor[se] <ABA>;} {#var blockcolor[se] <G03>;};
    #if {"$rozk2[sw]" != "  "} {#var blockcolor[sw] <ABA>;} {#var blockcolor[sw] <G03>;};
    #if {"$rozk2[d]" != " "} {#var blockcolor[d] <ABA>;} {#var blockcolor[d] <G03>;};
    #if {"$rozk2[u]" != " "} {#var blockcolor[u] <ABA>;} {#var blockcolor[u] <G03>;};
    #if {"$rooa" != ""} {#var blockcolor[bind] <AAB><fff>} {#var blockcolor[bind] <G01><g01>};

    #screen clear square $screenrows-13 1 $screenrows $screencols;

    #draw <088><g08> rounded grid table $screenrows-8 1 $screenrows $screencols {<178>q;w;e;r;t;y;u;i;o;p;$blockcolor[nw]NW\n;$blockcolor[n]N\n;$blockcolor[ne]NE\n;$blockcolor[u]A\n} {;a;s;d;f;g;h;j;k;l;A;$blockcolor[w]W\n;$blockcolor[bind]LOK\n;$blockcolor[e]E\n;<FFA><aaa>BIND\n} {z;x;c;v;b;n;m;A;B;C;$blockcolor[sw]SW\n;$blockcolor[s]S\n;$blockcolor[se]SE\n;$blockcolor[d]V\n};
    #format microbarline {%h} {  $microhp[$state[hp]]  ## MN:$state[mana]/8 ST:$state[fatigue]/9 ## RO:$rozkazlevel/15 MA:$manewrlevel/5<088>  };
	#line ignore #sh {$microbarline'} {-15};
}
#alias {BottomBarMicro} {
	#math realFatigue {9 - $state[fatigue]};
	#sh {${bbBGC}H[@graph{$state[hp];6}${bbBGC}]F[@graph{$state[form];3}${bbBGC}]G[@graph{$state[stuffed];3}${bbBGC}]X[@graph{$total_kill_count;$postep_needed}${bbBGC}]<088>M[@graph{$manewrlevel;4}${bbBGC}]<088>} {-4};
	#sh {${bbBGC}S[@graph{$realFatigue;9}${bbBGC}]O[@graph{$state[encumbrance];5}${bbBGC}]P[@graph{$state[soaked];3}${bbBGC}]P[@graph{$state[improve];15}${bbBGC}]<088>R[@graph{$rozkazlevel;14}${bbBGC}]<088>} {-3};
	#sh {${bbBGC}M[@graph{$state[mana];8}${bbBGC}]P[@graph{$state[panic];5}${bbBGC}]U[@graph{$state[intox];10}${bbBGC}]G[@graph{$ConfigManager_Configs[globalnypostep];$ConfigManager_Configs[celpostepow]}${bbBGC}]<088>B[@graph{$blockselflevel;5}${bbBGC}]<088>} {-2};
};

#alias {BottomBarTinyFugue} {
	#nop RYSUJEMY ROZE PO LEWEJ;
	rysuj_roze;
	#nop RYSUJEMY CUSTOM EXITS;
	rysuj_customs;
	#nop RYSUJEMY PASKI GLOWNE;
	rysuj_paski;
	#nop RYSUJEMY CZAT;
	rysuj_czat;
	#nop RYSUJEMY PRZYCISKI WALKI;
	rysuj_przyciski;
	#nop RYSUJEMY PASEK FORMY;
	#nop rysuj_forme;
};

#alias rysuj_forme_standard {
	#unticker fpstick;
	#math {pozycjapaskaformy} {$screencols - 16};
	#line ignore #sh {${bbBGC}Forma : $state[form] Glod: $state[stuffed]<088>} {-4} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}Panika: $state[panic] Prag: $state[soaked]<088>} {-3} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}Ciezar: $state[encumbrance] Pija: $state[intox]<088>} {-2} {$pozycjapaskaformy};
};

#alias rysuj_forme_fps {
	#math {pozycjapaskaformy} {$screencols - 16};
	#line ignore #sh {${bbBGC}FPS [@graph{$fpsTestResultValue;2000}]<088>} {-4} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}LAG $_ping_ <088>} {-3} {$pozycjapaskaformy};
	#line ignore #sh {${bbBGC}MAP [$mapPongedValue<088>]} {-2} {$pozycjapaskaformy};
};

#var ptrybchodzeniasymbol {<128>W};
#alias rysuj_roze {
	#line ignore #sh {$bbBGC   <138>$rozk2[nw] $rozk2[n] $rozk2[ne] $rozk2[u] <088>} {-4} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[w]$rozk2[w] $ptrybchodzeniasymbol <138>$rozk2[e]$rozk2[e]  <088>} {-3} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[sw] $rozk2[s] $rozk2[se]$rozk2[d] <088>} {-2} {1};
};

#alias rysuj_przyciski {
	#math {pozycjaprzyciskow} {$screencols - 20};
	#line ignore #sh {${bbBGC}AT <088>$buttons[0][4][label]${bbBGC} <088>$buttons[0][3][label]${bbBGC} <088>$buttons[0][2][label]${bbBGC} <088>$buttons[0][1][label]${bbBGC}<088>} {-4} {$pozycjaprzyciskow};
	#line ignore #sh {${bbBGC}ZA <088>$buttons[1][4][label]${bbBGC} <088>$buttons[1][3][label]${bbBGC} <088>$buttons[1][2][label]${bbBGC} <088>$buttons[1][1][label]${bbBGC}<088>} {-3} {$pozycjaprzyciskow};
	#line ignore #sh {${bbBGC}OT <088>$buttons[2][4][label]${bbBGC} <088>$buttons[2][3][label]${bbBGC} <088>PLA${bbBGC} <088>$buttons[2][1][label]${bbBGC}<088>} {-2} {$pozycjaprzyciskow};
};

#alias rysuj_customs {
	#draw {<088>$bbBGC} tile $screenrows-3 12 $screenrows-1 22 $bbBGC<ffa>$roza_custom;
};


#alias rysuj_paski_standard {
	#math remainingArea {$screencols - (16 + 10 + 12)};
	#if {"$pmaxbarsize" == "auto"} {
		#math maxBarSize {($remainingArea / 3) - 10};
	} {
		#var maxBarSize $pmaxbarsize;
	};

	#math realFatigue {9 - $state[fatigue]};
	#sh {${bbBGC}HP: [@buildBar{$state[hp];6;$maxBarSize;ada}<088>${bbBGC}]  MA: [@buildBarGreen{$manewrlevel;5;$maxBarSize;bbb}<088>${bbBGC}]<088>} {-4} {23};
	#sh {${bbBGC}ST: [@buildBar{$realFatigue;9;$maxBarSize;ddd}<088>${bbBGC}]  RO: [@buildBar{$rozkazlevel;15;$maxBarSize;ccc}<088>${bbBGC}]<088>} {-3} {23};
	#sh {${bbBGC}MA: [@buildBar{$state[mana];8;$maxBarSize;aad}<088>${bbBGC}]  BL: [@buildBar{$blockselflevel;5;$maxBarSize;faa}<088>${bbBGC}]<088>} {-2} {23};
};

#alias rysuj_paski_graph {
	#math realFatigue {9 - $state[fatigue]};
	#sh {${bbBGC}HP [@graph{$state[hp];6}${bbBGC}]  FO [@graph{$state[form];3}${bbBGC}]  GL [@graph{$state[stuffed];3}${bbBGC}]  XP [@graph{$total_kill_count;$postep_needed}${bbBGC}]<088>  MA [@graph{$manewrlevel;4}${bbBGC}]<088>} {-4} {23};
	#sh {${bbBGC}ST [@graph{$realFatigue;9}${bbBGC}]  OB [@graph{$state[encumbrance];5}${bbBGC}]  PR [@graph{$state[soaked];3}${bbBGC}]  PO [@graph{$state[improve];15}${bbBGC}]<088>  RO [@graph{$rozkazlevel;14}${bbBGC}]<088>} {-3} {23};
	#sh {${bbBGC}MA [@graph{$state[mana];8}${bbBGC}]  PA [@graph{$state[panic];5}${bbBGC}]  UP [@graph{$state[intox];10}${bbBGC}]  GL [@graph{$ConfigManager_Configs[globalnypostep];$ConfigManager_Configs[celpostepow]}${bbBGC}]<088>  BL [@graph{$blockselflevel;5}${bbBGC}]<088>} {-2} {23};
};

#nop 1. current state;
#nop 2. max state;
#nop 3. max width;
#function getGraphValue {
	#if {%1 > 0} {
		#local curst {%1};
		#math stslot {%3 / %2};
		#math stmodu {%3 % %2};
		#math stlen {($curst * $stslot) + $stmodu};
	} {
		#math stlen 0;
	};
	#if {%1 == %2} {
		#math stlen %3;
	};

	#return {$stlen};
};

#nop 1. current state;
#nop 2. max state;
#nop 3. max width;
#nop 4. colour;
#function buildBar {
	#local outputbar {};

	#if {%1 > 0} {
		#local curst {%1};
		#math stslot {%3 / %2};
		#math stmodu {%3 % %2};
		#math stlen {($curst * $stslot) + $stmodu};
		#loop {1} {$stlen-1} {stl} {#local outputbar {$outputbar#};};
		#loop {$stlen} {%3} {stl}  {#local outputbar {$outputbar };};
	} {
		#loop {0} {%3-1} {stl} {#local outputbar {$outputbar };};
	};
	#if {%1 == %2} {
		#local outputbar {};
		#loop {1} {%3} {stl} {#local outputbar {$outputbar#};};
	};

	#return {<%4>$outputbar<088>};
};

#function buildBarGreen {
	#local outputbar {};

	#if {%1 > 0} {
		#local curst {%1};
		#math stslot {%3 / %2};
		#math stmodu {%3 % %2};
		#math stlen {($curst * $stslot) + $stmodu};
		#loop {1} {$stlen-1} {stl} {#local outputbar {$outputbar#};};
		#loop {$stlen} {%3} {stl}  {#local outputbar {$outputbar };};
	} {
		#loop {0} {%3-1} {stl} {#local outputbar {$outputbar };};
	};
	#if {%1 == %2} {
		#local outputbar {};
		#loop {1} {%3} {stl} {#local outputbar {<ada>$outputbar#};};
	};

	#return {<%4>$outputbar<088>};
};


#alias prompt_pressed_mouse_button_one {
    #screen get ROWS sr;
    #screen get SPLIT_BOT_BAR sb;
    #math topButtonsFrameBorder {$sr - $sb};
    #nop var topButtonsFrameBorder 1;
    #nop if {%1 >= $topButtonsFrameBorder && %1 < $topButtonsFrameBorder+5 && %2 > 1 && %2 < $screencols} {};
        #math currRow {%1 - ($topButtonsFrameBorder)};
        #math currCol {($screencols - %2 + 1) / 4};

        #if {$mousepresseddebug} {
        	#line ignore #sh {<028>Mouse> <128>Button Click [$currRow] [$currCol]};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "wskazywanie"} {
            #local tmpflagi $pwskazywanie;
            #replace tmpflagi {A} {};
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                #replace tmpflagi {$buttons[$currRow][$currCol][value]} {};
            } {
                #local tmpflagi {${tmpflagi}$buttons[$currRow][$currCol][value]};
            };
            /conf atakflagi A${tmpflagi};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "zaslanianie"} {
            #local tmpflagi $pzaslonaflagi;
            #replace tmpflagi {A} {};
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                #replace tmpflagi {$buttons[$currRow][$currCol][value]} {};
            } {
                #local tmpflagi {${tmpflagi}$buttons[$currRow][$currCol][value]};
            };
            /conf zaslonaflagi A${tmpflagi};
        };
        #if {"$buttons[$currRow][$currCol][type]" == "switch"} {
            #if {"$buttons[$currRow][$currCol][status]" == "on"} {
                $buttons[$currRow][$currCol][cmdoff];
            } {
                $buttons[$currRow][$currCol][cmdon];
            }
        };
};

#nop POSTEP; #event {VARIABLE UPDATED ConfigManager_Configs[celpostepow]} {
	#if {"$ConfigManager_Configs[stylpaskow]" == "graph" || $ConfigManager_Configs[pasek]" == "micro"} {
    	rysuj_paski;
    };
}
#nop PROMPT; #event {VARIABLE UPDATED postep_needed} {
	#if {"$ConfigManager_Configs[stylpaskow]" == "graph" || $ConfigManager_Configs[pasek]" == "micro"} {
    	rysuj_paski;
    };
};
#nop PROMPT; #event {VARIABLE UPDATED total_kill_count} {
	#if {"$ConfigManager_Configs[stylpaskow]" == "graph" || $ConfigManager_Configs[pasek]" == "micro"} {
    	rysuj_paski;
    };
}

#event {VARIABLE UPDATED pwskazywanie} {
    #regex {$pwskazywanie} {z} {
        #var buttons[0][4][label] {<BAA><fff>ZAB<088>};
        #var buttons[0][4][status] {on};
    } {
        #var buttons[0][4][label] {ZAB};
        #var buttons[0][4][status] {off};
    };
    #regex {$pwskazywanie} {w} {
        #var buttons[0][3][label] {<BAB><fff>WSK<088>};
        #var buttons[0][3][status] {on};
    } {
        #var buttons[0][3][label] {WSK};
        #var buttons[0][3][status] {off};
    };
    #regex {$pwskazywanie} {r} {
        #var buttons[0][2][label] {<ABB><fff>ROZ<088>};
        #var buttons[0][2][status] {on};
    } {
        #var buttons[0][2][label] {ROZ};
        #var buttons[0][2][status] {off};
    };
    #regex {$pwskazywanie} {p} {
        #var buttons[0][1][label] {<FAA><fff>PRZ<088>};
        #var buttons[0][1][status] {on};
    } {
        #var buttons[0][1][label] {PRZ};
        #var buttons[0][1][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
};

#event {VARIABLE UPDATED pzaslonaflagi} {
    #regex {$pzaslonaflagi} {z} {
        #var buttons[1][4][label] {<AAB><fff>ZAS<088>};
        #var buttons[1][4][status] {on};
    } {
        #var buttons[1][4][label] {ZAS};
        #var buttons[1][4][status] {off};
    };
    #regex {$pzaslonaflagi} {w} {
        #var buttons[1][3][label] {<BAB><fff>WSK<088>};
        #var buttons[1][3][status] {on};
    } {
        #var buttons[1][3][label] {WSK};
        #var buttons[1][3][status] {off};
    };
    #regex {$pzaslonaflagi} {r} {
        #var buttons[1][2][label] {<ABB><fff>ROZ<088>};
        #var buttons[1][2][status] {on};
    } {
        #var buttons[1][2][label] {ROZ};
        #var buttons[1][2][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
};

#event {VARIABLE UPDATED bronstatus} {
    #if {"$bronstatus" == "on"} {
        #var buttons[2][4][label] {<FAA><fff>BRO<088>};
        #var buttons[2][4][status] {on};
        #if {"$ConfigManager_Configs[pasek]" == "medium"} {
	    	rysuj_przyciski;
    	};
    } {
        #var buttons[2][4][label] {BRO};
        #var buttons[2][4][status] {off};
        #if {"$ConfigManager_Configs[pasek]" == "medium"} {
	    	rysuj_przyciski;
    	};
    };
};

#event {VARIABLE UPDATED pzdjeciezaslon} {
    #if {"$pzdjeciezaslon" == "auto"} {
        #var buttons[1][1][label] {<AAB><fff>ZDE<088>};
        #var buttons[1][1][status] {on};
    } {
        #var buttons[1][1][label] {ZDE};
        #var buttons[1][1][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
}

#event {VARIABLE UPDATED ptrybchodzenia} {
	#switch {$ptrybchodzenia} {
		#case {0} {#var ptrybchodzeniasymbol <128>W};
		#case {1} {#var ptrybchodzeniasymbol <138>P};
		#case {2} {#var ptrybchodzeniasymbol <118>K};
	};
	#if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_roze;
    };

}

#event {VARIABLE UPDATED lampastatus} {
	#if {"$lampastatus" == "on"} {
        #var buttons[2][3][label] {<FFA><aaa>LAM<088>};
        #var buttons[2][3][status] {on};
	} {
        #var buttons[2][3][label] {LAM};
        #var buttons[2][3][status] {off};
	};
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
}

#event {VARIABLE UPDATED ppowtorzenter} {
    #if {"$ppowtorzenter" == "on"} {
        #var buttons[2][1][label] {<CFF><aaa>ENT<088>};
        #var buttons[2][1][status] {on};
    } {
        #var buttons[2][1][label] {ENT};
        #var buttons[2][1][status] {off};
    };
    #if {"$ConfigManager_Configs[pasek]" == "medium"} {
    	rysuj_przyciski;
    };
}
