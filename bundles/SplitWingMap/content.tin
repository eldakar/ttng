#nop #########################################################################;
#nop Czy wyswietlac przyciski bindow lokacyjnych i quickbindow na mapie VT;
#nop #########################################################################;
//confadd splitmap bindynamapie wlacz;
    #alias {bindynamapie} {
        #if {"$ConfigManager_Configs[bindynamapie]" == ""} {
            #local ret @bundleops_default_config{bindynamapie};
        };
    };
#nop #########################################################################;
#nop Event map updated vtmap;
#nop rooa - wartosc oddawana przy evencie map room enter;
#nop #########################################################################;
#list event_handler_map_updated_vtmap add {raidmap_map_updated_vtmap};
    #alias raidmap_map_updated_vtmap {
        #if {"$ConfigManager_Configs[bindynamapie]" == "wlacz" && "$rooa" != ""} {
            SplitWingMap_LabelBind $rooa;
        };
    };

#alias SplitWingMap_Offset {
    #math offsety 1;
    #screen get cols screencols;
    #screen get rows screenrows;
    #math offsetx {$screencols - $global_bundle_slots_values[prompt][right] + 1}; #nop +2;
    #math offseth {$screencols};
    #math offsetw {$screenrows - $global_bundle_slots_values[prompt][bottom] - $ConfigManager_Configs[RaidSize] - 1};
    #math raidoffsety {$praidsize + 1};
    #math raidoffsetw {$screenrows - $global_bundle_slots_values[prompt][bottom]-1};
    #math raidoffsetx {$screencols - $global_bundle_slots_values[prompt][right]}; #nop + 1;
    #map offset $offsety $offsetx $offsetw $offseth;
};

#alias {SplitWingMapWidth} {
    #var global_bundle_slots_values[prompt][right] $ConfigManager_Configs[SplitWingMapWidth];
    #var splitwingmapwidthused 1;
    #if {$ConfigManager_Configs[SplitWingMapWidth] < 1} {
        #map flag vtmap off;
        #unevent {VARIABLE UPDATED objectnumsRefreshed} {RaidBar_refresh};  #nop RAIDTABLE HOOK;
    } {
        #map flag vtmap on;
        #event {VARIABLE UPDATED objectnumsRefreshed} {RaidBar_refresh};    #nop RAIDTABLE HOOK;
    };
    SplitRefreshRequestRefresh;
};

#alias {RaidSize} {
    #if {$ConfigManager_Configs[RaidSize] > 0} {
        #var praidsize {$screenrows - $global_bundle_slots_values[prompt][bottom] - $ConfigManager_Configs[RaidSize]};
    } {
        #math praidsize {$screenrows - $global_bundle_slots_values[prompt][bottom] - 5};
    };
    #math raidoffsety {$praidsize + 1};

    #nop CZY MUSI TU?  RaidBar_refresh;
    SplitWingMap_refresh;
}

#alias {doubleclick} {
    #switch {"$MacroManager_Macros[doubleclick]"} {
        #case {"walk"} {#alias raidmap_map_double_clicked_mouse_button_one {raidmap_map_double_clicked_mouse_button_one_walk}};
        #case {"run"} {#alias raidmap_map_double_clicked_mouse_button_one {raidmap_map_double_clicked_mouse_button_one_run}};
        #case {"path"} {#alias raidmap_map_double_clicked_mouse_button_one {raidmap_map_double_clicked_mouse_button_one_path}};
        #case {"ball"} {#alias raidmap_map_double_clicked_mouse_button_one {raidmap_map_double_clicked_mouse_button_one_ball}};
        #default {
            #alias raidmap_map_double_clicked_mouse_button {raidmap_map_double_clicked_mouse_button_one_ball};
            #var MacroManager_Macros[doubleclick] {ball};
        };
    };
};

#alias {RaidBar_refresh} {
    #if {$praidsize > 0 && "$raidbartable" != "" && $raidoffsetw > 0 && $splitwingmapwidthused == 1} {
        #draw tile $raidoffsety $raidoffsetx+1 $raidoffsetw $screencols <088>$raidbartable; #nop ORG;
        #sh {<118>X<088>} {$raidoffsetw} {$raidoffsetx+1};
    };
}

#alias {SplitWingMap_refresh} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 0} {
        #math global_bundle_slots_values[prompt][right] {$ConfigManager_Configs[SplitWingMapWidth] + 0};
        #if {$global_bundle_slots_values[prompt][right] < 1} {
            #var ConfigManager_Configs[SplitWingMapWidth] {10};
            #var global_bundle_slots_values[prompt][right] {10};
        };
        SplitWingMap_Offset;
        RaidBar_refresh;
    };
};

#nop UZYWANY TYLKO PRZY OCENIE SPRZETU ??;
#alias {SplitWingMap_Popup} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw violet circled box $offsetw-15 $offsetx+3 $offsetw-7 $offseth-3 <088>%0;
    };
};


#alias {SplitWingMap_Journal_1} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw tile $offsety $offsetx $offsety $offseth <088>%0;
    };
};

#alias {SplitWingMap_Journal_2} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw tile $offsety $offsetx $offsety+1 $offseth <088>%0;
    };
};

#alias {SplitWingMap_Journal_3} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw tile $offsety $offsetx $offsety+2 $offseth <088>%0;
    };
};

#alias {SplitWingMap_Journal_4} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw tile $offsety $offsetx $offsety+3 $offseth <088>%0;
    };
};

#alias {SplitWingMap_Journal_5} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
        #draw tile $offsety $offsetx $offsety+4 $offseth <088>%0;
    };
};


#nop NIEUZYWANE;    #unalias {SplitWingMap_Board} {
                        #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
                            #draw jeweled box $offsety+10 $offsetx+3 $offsetw-5 $offseth-3 <088>%0;
                        };
                    };

#nop NIEUZYWANE;    #unalias {SplitWingMap_LabelMap} {
                        #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
                            #draw circled box $offsety+2 $offsetx+3 $offsety+4 $offseth-3 <088>%0<088>;
                        };
                    };

#nop NIEUZYWANY;    #unalias {SplitWingMap_Journal} {
                        #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
                                #nop                   Y          X        Y + H        X + W            ;
                                #nop draw jeweled box $offsety+34 $offsetx+3 $offsetw-5 $offseth-3 <088>%0;
                                #math maxheight { ($offsetw - 5 - $offsety) / 3 };
                                #math bot_row {$offsetw - ($offsetw / 5)};
                                #draw bottom tile $bot_row $offsetx $offsetw $offseth-3 <088>%0;
                        };
                    };

#alias {SplitWingMap_LabelBind} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
            #draw <aaf> rounded box $offsetw-3 $offsetx $offsetw-1 $offsetx+12 {<AAF><178> LOKACYJNY <088>};

            #format {bindlen} {%L} {%0};
            #if {$bindlen > $ConfigManager_Configs[SplitWingMapWidth]-14} {
                #draw tile $offsetw-2 $offsetx+13 $offsetw-1 $screencols-1 <168>%0<088>;
            } {
                #sh {<168> %0<088>} {$offsetw-2} {$offsetx+13};
            };
    };
};

#alias {SplitWingMap_Warning} {
    #if {$ConfigManager_Configs[SplitWingMapWidth] > 20} {
            #draw <faa> rounded box $offsetw-6 $offsetx $offsetw-4 $offsetx+12 {<FAA><178> QUICKBIND <088>};

            #format {bindlen} {%L} {%0};
            #if {$bindlen > $ConfigManager_Configs[SplitWingMapWidth]-14} {
                #draw tile $offsetw-5 $offsetx+13 $offsetw-4 $screencols-1 <118>%0<088>;
            } {
                #sh {<118> %0<088>} {$offsetw-5} {$offsetx+13};
            };
    };
};


#var topdrag 0

#alias {raidmap_pressed_mouse_button_one}
{
    #screen get SPLIT_BOT_BAR top;
    #screen get SPLIT_RIGHT_BAR right;
    #screen get cols cols;
    #math curright {$cols - %2 + 1};
    #math curbott {$screenrows - %1};
    #var {o_x} %1;
    #var {o_y} %2;

    #if {$right == $curright}
    {
       #var rightdrag 1
    };
    #if {$curright < $right} {
        #if {$curbott > $top && $o_x > 0} {
            #var mapcenter 1;
        };
    };

    #nop sh {curright = $curright | curbott = $curbott | right = $right | top = $top | topdrag: $topdrag rightdrag: $rightdrag map: $mapcenter buttons $mapbuttons | chatdrag: $chatdrag};
}

#alias raidmap_released_mouse_button_one
{
    #var topdrag 0;
    #var rightdrag 0;
    #var mapcenter 0;
    #var chatdrag 0;
}

#alias raidmap_moved_mouse_button_one
{
    #if {$rightdrag}
    {
        #math boooom {$cols - %2};
        /conf SplitWingMapWidth $boooom;
    };
    #if {$mapcenter} {
        #var d_x %1;
        #var d_y %2;
        #math {c_x} {$o_x - $d_x};
        #math {c_y} {$d_y - $o_y};
        #map center $c_y $c_x 0;
    }
}

#alias raidmap_scrolled_mouse_wheel_up
{
    #if {%2 == 0 || %2 == 1} {#cursor history prev} {
        #buffer up 1;
    }
}

#alias raidmap_scrolled_mouse_wheel_down
{
    #if {%2 == 0 || %2 == 1} {#cursor history next} {
        #buffer down 1;
    }
}

#alias raidmap_map_double_clicked_mouse_button_one {
    #map center;
    #map goto {%1};
    #var pathinprogress 1;
};

#alias raidmap_map_double_clicked_mouse_button_one_run {
    #map center;
    #map run {%1} {0.25};
    #var pathinprogress 1;
};

#alias raidmap_map_double_clicked_mouse_button_one_walk {
    #map center;
    #map run {%1} {1.50};
    #var pathinprogress 1;
};


#alias raidmap_map_double_clicked_mouse_button_one_ball {
    #map center;
    #map goto {%1};
};

#alias raidmap_map_double_clicked_mouse_button_one_path {
    #map find {%1};
};

#alias raidmap_attacks {
    #draw <088><118> rounded table $offsetw-25 $offseth-5 $offsetw-13 $offseth {<FAA><fff>CEL\n} {<AAB><fff>OBR\n} {<ABA><fff>EXP\n};
}

#alias raidmap_poczta_on {
    #draw <088><118> rounded box $offsety $screencols-7 $offsety+2 $offseth {<FAA><fff>POCZTA};
};

#alias raidmap_poczta_off {
    #draw blanked box $offsety $screencols-7 $offsety+2 $offseth {};
};

#alias poczta_start {
    #ticker poczta {raidmap_poczta_on;#delay {1} {raidmap_poczta_off}} {2};
};

#alias {/p} {
    #unticker poczta;
    raidmap_poczta_off;
} {1};
