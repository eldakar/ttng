#event {IAC SB GMCP gmcp_msgs IAC SE} {
	#var GMCPMSGS {%0};
	#regex {$GMCPMSGS[text]} {{.*}\\n$} {
		#var GMCPMSGS[text] {&1};
	};
	#switch {"$GMCPMSGS[type]"} {
		#case {"comm"} {
			#replace {GMCPMSGS[text]} {\\n} {};

			#line sub variables #line strip {#var commtext $GMCPMSGS[text];};
			#regex {$commtext} {%w {dudni|nuci|mowi|mruczy}%*: %*} {
				#foreach {*duch[]} {dydydy} {
					#var shitTODOvar {$duch[$dydydy][desc]_};
					#if {"$duch[$dydydy][desc]" == "&1" && "$postac[$shitTODOvar][gildia]" != "" && "$postac[$shitTODOvar][gildia]" != "NPC"} {
						#line sub variables #line strip {topchat_add_static &1: &4;};
					};
				};
			} {
				#regex {$commtext} {Mowisz%*:} {
					#line sub variables #line strip {topchat_add_static Mowisz: &2};
				};
			};

			#sh {$GMCPMSGS[text]};
		};
		#case {"emotes"} {
			#replace {GMCPMSGS[text]} {\\n} {};
			#sh {$GMCPMSGS[text]};
		};
		#case {"notification.mail"} {Highlights_post $GMCPMSGS[text];poczta_start};
		#case {"notification.knowledge"} {Highlights_knowledge $GMCPMSGS[text]};
		#case {"room.exits"} {
			#replace {GMCPMSGS[text]} {\\n} {};			
			#sh {$GMCPMSGS[text]};
			#line substitute {variables} {#line strip {#var current_room_exits {$GMCPMSGS[text]}}};
#nop DEBUG;	#line substitute {variables} {#line strip {#var gmcp_exits {$GMCPMSGS[text]}}};
			#if {$MAPPER_SYNC == 0} {findme_exits};

			#if {"$ConfigManager_Configs[pasek]" == "medium"} {
		    	rysuj_roze;
		    	rysuj_customs;
			};
		    #if {$discovery == 1} {
		    	/disc;
		    };
		};
		#case {"room.short"} {
			#var ilecial 0;
			#var tmptext {$GMCPMSGS[text]};
			#regex {$tmptext} {{.*}\\n$} {
				#var GMCPMSGS[text] {&1};
				#var outputtext {&1};
			} {
				#var outputtext {$tmptext};
			};
			#replace {outputtext} {\\n} {};
#nop DEBUG;	#line substitute {variables} {#line strip {
				#var gmcp_short {$outputtext};
			}};
			#if {$bind_found > 0} {
				SplitWingMap_LabelBind $bind_run;
			};
			#sh {$outputtext};
			#nop BIND OR COMMAND;
		    #if {"$rooa" != ""} {$rooa};

			#line strip {#var temp_short {$outputtext}};
			#line substitute {variables} {
				#if {$ROOM_INFO[map][x] > 0} {} {#line strip {#var current_room_short_desc {$outputtext}}};
			};
			#if {$MAPPER_SYNC == 0} {findme_short};
			#if {$trytomove > 0} {
        		#var trytomove 0;
			    #map get roomvnum roomId;
    		};
		};
		#case {"combat.team"} {
			#var outputtext {$GMCPMSGS[text]};
			#replace {outputtext} {\\n} {};
			#sh $outputtext;
		};
		#case {"combat.others"} {
			#var outputtext {$GMCPMSGS[text]};
			#replace {outputtext} {\\n} {};
			#sh $outputtext;
		};
		#case {"combat.avatar"} {
			#var outputtext {$GMCPMSGS[text]};
			#replace {outputtext} {\\n} {};
			#sh $outputtext;
			#delay {wwalce} {
				#sh {<afa>Odsapnales po walce.<088>};
				#if {"$pplaszcz" == "wlaczony"} {
					qb otul sie plaszczem;
				};
			} {34};
		};	
		#case {"room.contents.object"} {
			#var outputtext {$GMCPMSGS[text]};
			#replace {outputtext} {\\n} {};
			#var roomcontentsData {$outputtext};
			#sh $outputtext;			

			#replace roomcontentsData {\.} {};
			#replace roomcontentsData {{, | i }} {;};
			#format {roomcontentsData} {%l} {$roomcontentsData};  
			#list roomcontentsList create {$roomcontentsData};
			#var ilecial 0;
			#foreach {*roomcontentsList[]} {_rcd} {
				#regex {$roomcontentsList[$_rcd]} {cialo} {
			  		#math ilecial {$ilecial + 1};
			  	};
			};
		  	#if {$ilecial > 0} {
		  		#line ignore #showme {Widzisz <ffa>$ilecial cial<088>.};
		  	};
		};
		
		#default {
			#sh $GMCPMSGS[text];
		};
	};		
};
